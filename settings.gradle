gradle.ext.modules = []
gradle.ext.addonInfo = new StringBuilder(512)
gradle.ext.localProps = new Properties()
gradle.ext.enableGoogleServices = (System.getenv('NO_GS') == null)
        && file('fermata/google-services.json').isFile()
def localPropsFile = file('local.properties')
def exoRoot = file('depends/ExoPlayer')

if (localPropsFile.isFile()) {
    gradle.ext.localProps.load(localPropsFile.newDataInputStream())
} else {
    gradle.ext.localProps.setProperty('storeFile', file('res/test.ks').absolutePath)
    gradle.ext.localProps.setProperty('keyAlias', 'test')
    gradle.ext.localProps.setProperty('keyPassword', 'fermata')
    gradle.ext.localProps.setProperty('storePassword', 'fermata')
}

if (exoRoot.isDirectory()) {
    gradle.ext.exoplayerRoot = exoRoot
    gradle.ext.exoplayerModulePrefix = 'exoplayer-'
    apply from: new File(exoRoot, 'core_settings.gradle')
}

rootProject.name = 'Fermata Music Player'
include ':fermata'
include ':control'
include ':utils'
project(':utils').projectDir = file('depends/utils')

file('modules').eachDir {
    def name = ':' + it.name
    if ((':exoplayer' == name) && !exoRoot.isDirectory()) return
    if ((':gdrive' == name) && !gradle.ext.enableGoogleServices) return
    gradle.ext.modules.add(name)
    include name
    project(name).projectDir = it
}
