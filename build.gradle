ext {
    VERSION_CODE = 255
    VERSION_NAME = "2.0.0"
    SDK_MIN_VERSION = 29
    SDK_TARGET_VERSION = 36
    SDK_COMPILE_VERSION = 36
    localProps = gradle.ext.localProps

    ANDROID_MATERIAL_VERSION = '1.13.0'
    ANDROID_PLAY_CORE_FD_VERSION = '2.1.0'
    ANDROIDX_CORE_VERSION = '1.12.0'
    ANDROIDX_MEDIA_VERSION = '1.7.1'
    ANDROIDX_APPCOMPAT_VERSION = '1.7.1'
    ANDROIDX_CONSTRAINTLAYOUT_VERSION = '2.2.1'
    ANDROIDX_SWIPEREFRESHLAYOUT_VERSION = '1.1.0'
    GUAVA_VERSION = '33.5.0-android'

    addons = [
            [
                    name       : 'translate',
                    icon       : 'translate',
                    class      : 'me.aap.fermata.addon.TranslateAddon',
                    hasSettings: false
            ],
            [
                    name   : 'subgen',
                    icon   : 'subgen',
                    class  : 'me.aap.fermata.addon.SubGenAddon',
                    depends: 'exoplayer,translate'
            ],
    ]
}

buildscript {

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.1'
        classpath 'com.google.gms:google-services:4.4.4'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

subprojects {
    def isDynFeature = false

    if ('fermata' == name) {
        apply plugin: 'com.android.application'
    } else if (gradle.ext.modules.contains(':' + name)) {
        isDynFeature = true
        apply plugin: 'com.android.dynamic-feature'
    } else {
        apply plugin: 'com.android.library'
    }

    android {
        compileSdk SDK_COMPILE_VERSION
        ndkVersion = '29.0.14206865'

        defaultConfig {
            minSdkVersion SDK_MIN_VERSION
            targetSdkVersion SDK_TARGET_VERSION
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            resConfigs 'en', 'ru', 'it', 'tr', 'de', 'pt', 'vi', 'pl', 'hr', 'ja', 'zh_TW', 'ko',
                    'fr', 'ro', 'ar', 'es', 'km'
        }

        if (isDynFeature) {
            flavorDimensions "version"

            productFlavors {
                mobile {
                    dimension "version"
                }
                auto {
                    dimension "version"
                    applicationIdSuffix '.auto' + project.getProperties().getOrDefault('APP_ID_SFX', '')
                }
            }
        }

        buildFeatures {
            buildConfig = true
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
            coreLibraryDesugaringEnabled = true
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }

        dependencies {
            coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test.ext:junit:1.3.0'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    }

    configurations.configureEach {
        resolutionStrategy {
            preferProjectModules()
            eachDependency { details ->
                if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava') {
                    details.useVersion GUAVA_VERSION
                }
            }
        }
    }
}

gradle.projectsEvaluated {
    def addons = new ArrayList<HashMap>(ext.addons)
    gradle.ext.modules.forEach {
        def p = project(it)
        if (p.ext.has('addons')) {
            p.ext.addons.forEach {
                def info = new HashMap(it)
                info['module'] = p.name
                addons.add(info)
            }
        }
    }

    def addonInfo = new StringBuilder(1024)
    addonInfo.append('me.aap.utils.collection.CollectionUtils.sort(new me.aap.fermata.addon.AddonInfo[] {\n')
    addons.forEach {
        def name = it['name']
        addonInfo.append("  new me.aap.fermata.addon.AddonInfo(" +
                "\"${it.get('module', name)}\", \"${it['class']}\", " +
                "R.string.addon_name_${name}, R.drawable.${it['icon']}, " +
                "${it['order']}, ${it['hasSettings']}, ${it['enabled']}, " +
                "\"${it.get('depends', "")}\"),\n")
    }

    def infoStr = addonInfo.append('})').toString()
    project(':fermata').android.productFlavors.forEach {
        it.buildConfigField 'me.aap.fermata.addon.AddonInfo[]', 'ADDONS', infoStr
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}
