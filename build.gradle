ext {
    def abi = project.properties['ABI']
    VERSION_CODE = 79
    VERSION_NAME = "1.7.3"
    SDK_MIN_VERSION = 23
    SDK_TARGET_VERSION = 29
    SDK_COMPILE_VERSION = 29
    BUILD_TOOLS_VERSION = "29.0.2"
    ABI_FILTERS = (abi != null) ? abi.split(",") : ['arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64']
    localProps = gradle.ext.localProps

    ANDROID_MATERIAL_VERSION = '1.2.1'
    ANDROID_PLAY_CORE_VERSION = '1.9.0'
    ANDROIDX_CORE_VERSION = '1.3.2'
    ANDROIDX_MEDIA_VERSION = '1.2.0'
    ANDROIDX_APPCOMPAT_VERSION = '1.2.0'
    ANDROIDX_CONSTRAINTLAYOUT_VERSION = '2.0.1'
}

buildscript {

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath 'com.google.gms:google-services:4.3.4'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://dl.bintray.com/videolan/Android" }
    }
}

subprojects {
    def isDynFeature = false

    switch (name) {
        case 'fermata':
        case 'control':
            apply plugin: 'com.android.application'
            break
        default:
            if (gradle.ext.modules.contains(':' + name)) {
                isDynFeature = true
                apply plugin: 'com.android.dynamic-feature'
            } else {
                apply plugin: 'com.android.library'
            }
    }

    android {
        compileSdkVersion SDK_COMPILE_VERSION
        buildToolsVersion BUILD_TOOLS_VERSION

        defaultConfig {
            minSdkVersion SDK_MIN_VERSION
            targetSdkVersion SDK_TARGET_VERSION
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            resConfigs "en", "ru"

            ndk {
                abiFilters = ABI_FILTERS
            }
        }

        if (isDynFeature) {
            flavorDimensions "version"

            productFlavors {
                mobile {
                    dimension "version"
                }
                auto {
                    dimension "version"
                    applicationIdSuffix '.auto'
                }
            }
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'androidx.test.ext:junit:1.1.2'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    }
}

gradle.projectsEvaluated {
    gradle.ext.modules.forEach {
        def p = project(it)

        if (p.ext.has('addons')) {
            p.ext.addons.forEach {
                if (gradle.ext.addonInfo.length() == 0) {
                    gradle.ext.addonInfo.append('new me.aap.fermata.addon.AddonInfo[] {\n')
                } else {
                    gradle.ext.addonInfo.append(',\n')
                }

                gradle.ext.addonInfo.append(
                        "new me.aap.fermata.addon.AddonInfo(\"${p.name}\", \"${it['class']}\", R.string.addon_name_${it['name']}, R.drawable.${it['icon']})")
            }
        }
    }

    if (gradle.ext.addonInfo.length() != 0) {
        gradle.ext.addonInfo.append('\n}')
        project(':fermata').android.productFlavors.forEach {
            it.buildConfigField 'me.aap.fermata.addon.AddonInfo[]', 'ADDONS', gradle.ext.addonInfo.toString()
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
