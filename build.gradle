ext {
    VERSION_CODE = 254
    VERSION_NAME = "1.9.10"
    SDK_MIN_VERSION = 23
    SDK_TARGET_VERSION = 35
    SDK_COMPILE_VERSION = 35
    localProps = gradle.ext.localProps

    ANDROID_MATERIAL_VERSION = '1.12.0'
    ANDROID_PLAY_CORE_FD_VERSION = '2.1.0'
    ANDROIDX_CORE_VERSION = '1.12.0'
    ANDROIDX_MEDIA_VERSION = '1.7.0'
    ANDROIDX_APPCOMPAT_VERSION = '1.7.1'
    ANDROIDX_CONSTRAINTLAYOUT_VERSION = '2.2.1'
    ANDROIDX_SWIPEREFRESHLAYOUT_VERSION = '1.1.0'
    GUAVA_VERSION = '33.4.0-android'
}

buildscript {

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.10.1'
        classpath 'com.google.gms:google-services:4.4.2'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

subprojects {
    def isDynFeature = false

    if ('fermata' == name) {
        apply plugin: 'com.android.application'
    } else if (gradle.ext.modules.contains(':' + name)) {
        isDynFeature = true
        apply plugin: 'com.android.dynamic-feature'
    } else {
        apply plugin: 'com.android.library'
    }

    android {
        compileSdk SDK_COMPILE_VERSION
        ndkVersion = "25.2.9519653"

        defaultConfig {
            minSdkVersion SDK_MIN_VERSION
            targetSdkVersion SDK_TARGET_VERSION
            vectorDrawables.useSupportLibrary = true
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            resConfigs 'en', 'ru', 'it', 'tr', 'de', 'pt', 'vi', 'pl', 'hr', 'ja', 'zh_TW', 'ko',
                    'fr', 'ro', 'ar'
        }

        if (isDynFeature) {
            flavorDimensions "version"

            productFlavors {
                mobile {
                    dimension "version"
                }
                auto {
                    dimension "version"
                    applicationIdSuffix '.auto' + project.getProperties().getOrDefault('APP_ID_SFX', '')
                }
            }
        }

        buildFeatures {
            buildConfig = true
        }

        compileOptions {
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
            coreLibraryDesugaringEnabled = true
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }

        dependencies {
            coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'
        }
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test.ext:junit:1.2.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    }

    configurations.configureEach {
        resolutionStrategy {
            preferProjectModules()
            eachDependency { details ->
                if (details.requested.group == 'com.google.guava' && details.requested.name == 'guava') {
                    details.useVersion GUAVA_VERSION
                }
            }
        }
    }
}

gradle.projectsEvaluated {
    gradle.ext.modules.forEach {
        def p = project(it)

        if (p.ext.has('addons')) {
            p.ext.addons.forEach {
                if (gradle.ext.addonInfo.length() == 0) {
                    gradle.ext.addonInfo.append('me.aap.utils.collection.CollectionUtils.sort(new me.aap.fermata.addon.AddonInfo[] {\n')
                } else {
                    gradle.ext.addonInfo.append(',\n')
                }

                gradle.ext.addonInfo.append("new me.aap.fermata.addon.AddonInfo(\"${p.name}\", \"${it['class']}\", R.string.addon_name_${it['name']}, R.drawable.${it['icon']}, ${it['order']}, ${it['enabled']})")
            }
        }
    }

    if (gradle.ext.addonInfo.length() != 0) {
        gradle.ext.addonInfo.append('\n})')
        project(':fermata').android.productFlavors.forEach {
            it.buildConfigField 'me.aap.fermata.addon.AddonInfo[]', 'ADDONS', gradle.ext.addonInfo.toString()
        }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}
